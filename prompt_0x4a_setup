#
# A colorful, friendly, multiline theme with some handy features.
#
# Authors:
#   Paul Gideon Dann <pdox4a@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Features:
#   - Simple VCS branch, staged, and unstaged indication.
#   - Prompt character is different in a VCS repository.
#   - Last command exit status is displayed when non-zero.
#

function +vi-set_novcs_prompt_symbol {
  _prompt_ox4a_symbol='>'
}

function +vi-set_vcs_prompt_symbol {
  _prompt_ox4a_symbol='Â±'
}

function +vi-git_precmd {
  # Check for untracked files, since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    hook_com[unstaged]+='%F{green}?%f'
  fi
}

function prompt_ox4a_precmd {
  # Replace '/home/<user>' with '~'.
  _prompt_ox4a_pwd="${PWD/#$HOME/~}"
  vcs_info
}

function prompt_ox4a_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz vcs_info
  autoload -Uz add-zsh-hook

  # Add hook to set up prompt parameters before each command.
  add-zsh-hook precmd prompt_ox4a_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%F{green}...%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format '%F{yellow}--- COMMAND ---%f'

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' formats ' on %F{magenta}%b%f%c%u'
  zstyle ':vcs_info:*' actionformats ' on %F{magenta}%b%f%c%u %F{yellow}(%a)%f'
  zstyle ':vcs_info:*' stagedstr '%F{green}+%f'
  zstyle ':vcs_info:*' unstagedstr '%F{green}!%f'

  # Set vcs_info hooks.
  # NOTE: Prior to Zsh v4.3.12, there are no static hooks, no vcs_info_hookadd
  #       function, and no 'no-vcs' hook.
  zstyle ':vcs_info:*+start-up:*' hooks set_novcs_prompt_symbol
  zstyle ':vcs_info:git*+set-message:*' hooks set_vcs_prompt_symbol git_precmd
  zstyle ':vcs_info:*+set-message:*' hooks set_vcs_prompt_symbol

  # Define prompts.
  PROMPT='%(?..%F{red}%B-> [%?]%b%f
)%F{red}%n%f@%F{172}%m%f|%F{70}${_prompt_ox4a_pwd}%f${vcs_info_msg_0_}
%F{blue}${_prompt_ox4a_symbol}%f '
  RPROMPT='${editor_info[keymap]}'
  SPROMPT='zsh: correct %F{magenta}%R%f to %F{green}%r%f [nyae]? '
}

prompt_ox4a_setup "$@"
